[PrefixDeclaration]
:		http://example.org/voc#
ais:		http://example.org/ais#
owl:		http://www.w3.org/2002/07/owl#
rdf:		http://www.w3.org/1999/02/22-rdf-syntax-ns#
xml:		http://www.w3.org/XML/1998/namespace
xsd:		http://www.w3.org/2001/XMLSchema#
obda:		https://w3id.org/obda/vocabulary#
rdfs:		http://www.w3.org/2000/01/rdf-schema#

[MappingDeclaration] @collection [[
mappingId	MappingVesselIdentifier
target		:ais-session/vessel/{mmsi} a :Vessel ; ais:mmsi {mmsi}^^xsd:integer . 
source		SELECT mmsi FROM metadata;

mappingId	MappingVesselhasMetadata
target		:ais-session/vessel/{mmsi} ais:hasMetadata :ais-session/metadata/{mmsi}/{time} . 
source		SELECT mmsi, time FROM metadata;

mappingId	MappingVesselhasLocation
target		:ais-session/vessel/{mmsi} ais:hasLocation :ais-session/location/{mmsi}/{time} . 
source		SELECT mmsi, time FROM location;

mappingId	MappingVesselLocations
target		:ais-session/location/{mmsi}/{time} a ais:Location ; ais:time {time}^^xsd:dateTime ; ais:mmsi {mmsi}^^xsd:integer ; ais:sog {sog}^^xsd:decimal ; ais:cog {cog}^^xsd:decimal ; ais:navStat {nav_stat}^^xsd:integer ; ais:rot {rot}^^xsd:decimal ; ais:posAcc {pos_acc}^^xsd:boolean ; ais:raim {raim}^^xsd:boolean ; ais:heading {heading}^^xsd:integer ; ais:lon {lon}^^xsd:double ; ais:lat {lat}^^xsd:double . 
source		SELECT mmsi, time, sog, cog, nav_stat, rot, pos_acc, raim, heading, lon, lat
			FROM location;

mappingId	MappingVesselMetadata
target		:ais-session/metadata/{mmsi}/{time} a ais:Metadata ; ais:time {time}^^xsd:dateTime ; ais:mmsi {mmsi}^^xsd:integer ; ais:hasShipType ais:ship_type/{ship_type} ; ais:name {name}^^xsd:string ; ais:destination {destination}^^xsd:string ; ais:draught {draught}^^xsd:decimal ; ais:imo {imo}^^xsd:integer ; ais:callSign {call_sign}^^xsd:string ; ais:eta {eta}^^xsd:integer ; ais:hasPosType ais:pos_type/{pos_type} ; ais:referencePointA {reference_point_a}^^xsd:integer ; ais:referencePointB {reference_point_b}^^xsd:integer ; ais:referencePointC {reference_point_c}^^xsd:integer ; ais:referencePointD {reference_point_d}^^xsd:integer . 
source		SELECT mmsi, time, imo, name, ship_type, destination, draught, call_sign, eta, pos_type, reference_point_a, reference_point_b, reference_point_c, reference_point_d
			FROM metadata;

mappingId	MappingShipType
target		ais:ship_type/{id} a ais:ShipTypeDescription ; ais:shipTypeDescription {description}^^xsd:string . 
source		SELECT id, description FROM ship_type_description;

mappingId	MappingPosType
target		ais:pos_type/{id} a ais:PosTypeDescription ; ais:posTypeDescription {description}^^xsd:string . 
source		SELECT id, description FROM pos_type_description ;
]]

